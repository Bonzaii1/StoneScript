//MUSHROOM FOREST SCRIPT
// -Bezerra, the Sage

var poisonHammer
poisonHammer = ascii
#<∞］#
##║##
##ˈ##
asciiend

func drawLoadout()
    draw.Box(3, 5, 10, 5, #FFFF00, 1)
    >`5,7,#blue,@item.left@
    >`5,9,#blue,@item.right@
    >`5,15,#green,@poisonHammer@

func speak(message)
    >o-10,-2,#red,@message@

func setAttack(itemLeft)
    equipL @itemLeft@
    equipR compound shield *9

func setAttackSpeed()
    equipL sword
    equipR sword *9

func setSpeed(itemRight)
    equipL triskelion
    equip @itemRight@

func setDash(itemLeft)
    equipL @itemLeft@
    equipR dashing shield

func setPickup(itemRight)
    equipL star
    equipR @itemRight@

var loops = 1
var run = 0
var recovering = false

?loc=Mushroom
    //drawLoadout()
    >`0,1,Current Time = @time.FormatCasual(totaltime)@
    >`0,2,Run # = @loops@
    >`65,0,Fastest Run = @time.FormatCasual(loc.bestTime)@ 
    ?run = 0
        >`0,0,Prev Run = First Run :p
    :
        >`0,0,Prev Run = @time.FormatCasual(run)@
    ?loc.loop
        loops++
    ?time <=1
        disable abilities
        disable loadout input
        disable hud abu
        setSpeed("compound shield")
    ?time >= 0 & time <= (30*5)
        var emote = "@~@"
        speak("Shrooooooooms")
        >( @emote@
    ?foe
        ?foe = mushroom_boss | foe = mushroom_boss_fat | foe = mushroom_boss_skinny
            ?ai.paused | ai.enabled = false
                speak("Long ahh cutscene frfr :/")
                >( Ò,ó
            :
                >`65,1,Current State = @foe.state@
                >`0,2,Distance = @foe.distance@
                
                ?recovering = false & armor < 1
                    recovering = true
                :?recovering = false
                    ?foe = mushroom_boss_skinny   
                        speak("OH UR MINE NOW BOI!")
                        >( ʘ‿ʘ
                    ?foe.distance <= 5
                        ?foe.armor > 1
                            setAttack("poison hammer")
                        :
                            setAttackSpeed()
                    :?foe.distance > 5 & foe.distance <= 15
                        setDash("sword")
                    :?foe.distance > 15
                        setSpeed("compound shield")
                :?recovering = true
                    ?foe = mushroom_boss_fat
                        speak("WOAH THERE BUDDY")
                        equipL poison hammer
                        equipR compound shield
                        ?armor = maxarmor
                            recovering = false
                    :
                        ?foe.distance > 5 & foe.distance <= 15
                            setDash("sword")
                        :
                            setAttack("sword")
                        ?armor > 2
                            recovering = false
                    
        :
            >( ☉ ‿ ⚆
            ?foe.distance > 15
                setSpeed("compound shield")
            :?foe.distance > 10 & foe.distance <=15
                setDash("sword")
            :?foe.distance <=10
                ?foe.count > 1
                    equip bardiche
                :   
                    setAttack("sword *10")
    :?pickup.distance < 5
        setPickup("compound shield")
    :
        setSpeed("compound shield")
        

    ?ai.paused & foe.count = 0   
        run = totaltime
        speak("Absolutely dogged on kid >:)")