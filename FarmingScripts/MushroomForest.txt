//MUSHROOM FOREST SCRIPT
// -Bezerra, the Sage
func speak(message)
    >o-10,0,#red,@message@

func setAttack()
    equipL poison hammer *5
    equipR towering shield +2

func setOuroboros()
    equipL ouroboros
    equipR towering shield +2

var loops = 1
var run = 0
var fastest_run = 0
var timeSet = false

?loc=Mushroom
    >`0,1,Run # = @loops@
    ?run = 0
        >`0,0,Prev Run = First Run :p
        >`65,0,Fastest Run = First Run :p 
    :
        >`0,0,Prev Run = @time.FormatCasual(run)@
        >`65,0,Fastest Run = @time.FormatCasual(fastest_run)@ 
    ?loc.begin
        disable abilities
        disable loadout input
        disable hud abu
        setAttack()
    ?loc.loop
        loops++
        disable abilities
        disable loadout input
        disable hud abu
        ?hp < maxhp
            setOuroboros()
    ?time >= 0 & time <= (30*5)
        speak("Shrooooooooms @u@")

    ?foe
        ?foe = mushroom_boss | foe = mushroom_boss_fat | foe = mushroom_boss_skinny
            ?ai.paused
                speak("Long ahh cutscene frfr :/")
                setOuroboros()
            :
                >`65,1,Current Boss = @foe.state@
                >`0,2,Distance = @foe.distance@
                     
                ?foe.distance <= 5
                    setAttack()
                :?foe.distance > 5
                    equipL poison hammer *5
                    equipR dashing shield
        :
            ?hp < maxhp
                setOuroboros()
            :
                equip bardiche
    :?pickup
        equipL star
        equipR towering shield +2
    :
        setOuroboros()
        

    ?ai.paused & foe.count = 0
        ?timeSet = false
            run = totaltime
            ?fastest_run = 0 | run < fastest_run
                fastest_run = run
            timeSet = true
        speak("Absolutely dogged on kid >:)")